[bits 32]

section .text

%macro ISR_NO_ERROR_CODE 1
	ISR%1:
	    push 0              ; push dummy error code
	    push %1             ; push interrupt number
	    jmp ISR_common
%endmacro

; cpu pushes an error code to the stack
%macro ISR_ERROR_CODE 1
	ISR%1:
	    push %1
	    jmp ISR_common
%endmacro


ISR_NO_ERROR_CODE 0
ISR_NO_ERROR_CODE 1
ISR_NO_ERROR_CODE 2
ISR_NO_ERROR_CODE 3
ISR_NO_ERROR_CODE 4
ISR_NO_ERROR_CODE 5
ISR_NO_ERROR_CODE 6
ISR_NO_ERROR_CODE 7
ISR_ERROR_CODE 8
ISR_NO_ERROR_CODE 9
ISR_ERROR_CODE 10
ISR_ERROR_CODE 11
ISR_ERROR_CODE 12
ISR_ERROR_CODE 13
ISR_ERROR_CODE 14
ISR_NO_ERROR_CODE 15
ISR_NO_ERROR_CODE 16
ISR_ERROR_CODE 17
ISR_NO_ERROR_CODE 18
ISR_NO_ERROR_CODE 19
ISR_NO_ERROR_CODE 20
ISR_ERROR_CODE 21
ISR_NO_ERROR_CODE 22
ISR_NO_ERROR_CODE 23
ISR_NO_ERROR_CODE 24
ISR_NO_ERROR_CODE 25
ISR_NO_ERROR_CODE 26
ISR_NO_ERROR_CODE 27
ISR_NO_ERROR_CODE 28
ISR_ERROR_CODE 29
ISR_ERROR_CODE 30
ISR_NO_ERROR_CODE 31

%assign i 32
%rep 224
	ISR_NO_ERROR_CODE i
	%assign i i+1
%endrep

extern ISR_dispatcher
ISR_common:
    pusha               ; pushes in order: eax, ecx, edx, ebx, esp, ebp, esi, edi

    xor eax, eax        ; push ds
    mov ax, ds
    push eax

    mov ax, 0x10        ; use kernel data segment
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax

    push esp            ; pass pointer to stack to C, so we can access all the pushed information
    call ISR_dispatcher
    add esp, 4

    pop eax             ; restore old segment
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax

    popa                ; pop what we pushed with pusha
    add esp, 8          ; remove error code and interrupt number
    iret                ; will pop: cs, eip, eflags, ss, esp

section .rodata
	global ISRs
	ISRs: dd ISR0, ISR1, ISR2, ISR3, ISR4, ISR5, ISR6, ISR7, ISR8, ISR9, ISR10, ISR11, ISR12, ISR13, ISR14, \
	ISR15, ISR16, ISR17, ISR18, ISR19, ISR20, ISR21, ISR22, ISR23, ISR24, ISR25, ISR26, ISR27, \
	ISR28, ISR29, ISR30, ISR31, ISR32, ISR33, ISR34, ISR35, ISR36, ISR37, ISR38, ISR39, ISR40, \
	ISR41, ISR42, ISR43, ISR44, ISR45, ISR46, ISR47, ISR48, ISR49, ISR50, ISR51, ISR52, ISR53, \
	ISR54, ISR55, ISR56, ISR57, ISR58, ISR59, ISR60, ISR61, ISR62, ISR63, ISR64, ISR65, ISR66, \
	ISR67, ISR68, ISR69, ISR70, ISR71, ISR72, ISR73, ISR74, ISR75, ISR76, ISR77, ISR78, ISR79, \
	ISR80, ISR81, ISR82, ISR83, ISR84, ISR85, ISR86, ISR87, ISR88, ISR89, ISR90, ISR91, ISR92, \
	ISR93, ISR94, ISR95, ISR96, ISR97, ISR98, ISR99, ISR100, ISR101, ISR102, ISR103, ISR104, ISR105, \
	ISR106, ISR107, ISR108, ISR109, ISR110, ISR111, ISR112, ISR113, ISR114, ISR115, ISR116, ISR117, ISR118, \
	ISR119, ISR120, ISR121, ISR122, ISR123, ISR124, ISR125, ISR126, ISR127, ISR128, ISR129, ISR130, ISR131, \
	ISR132, ISR133, ISR134, ISR135, ISR136, ISR137, ISR138, ISR139, ISR140, ISR141, ISR142, ISR143, ISR144, \
	ISR145, ISR146, ISR147, ISR148, ISR149, ISR150, ISR151, ISR152, ISR153, ISR154, ISR155, ISR156, ISR157, \
	ISR158, ISR159, ISR160, ISR161, ISR162, ISR163, ISR164, ISR165, ISR166, ISR167, ISR168, ISR169, ISR170, \
	ISR171, ISR172, ISR173, ISR174, ISR175, ISR176, ISR177, ISR178, ISR179, ISR180, ISR181, ISR182, ISR183, \
	ISR184, ISR185, ISR186, ISR187, ISR188, ISR189, ISR190, ISR191, ISR192, ISR193, ISR194, ISR195, ISR196, \
	ISR197, ISR198, ISR199, ISR200, ISR201, ISR202, ISR203, ISR204, ISR205, ISR206, ISR207, ISR208, ISR209, \
	ISR210, ISR211, ISR212, ISR213, ISR214, ISR215, ISR216, ISR217, ISR218, ISR219, ISR220, ISR221, ISR222, \
	ISR223, ISR224, ISR225, ISR226, ISR227, ISR228, ISR229, ISR230, ISR231, ISR232, ISR233, ISR234, ISR235, \
	ISR236, ISR237, ISR238, ISR239, ISR240, ISR241, ISR242, ISR243, ISR244, ISR245, ISR246, ISR247, ISR248, \
	ISR249, ISR250, ISR251, ISR252, ISR253, ISR254, ISR255
